!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!   M O R P H O L O G I C A L · T R A N S D U C E R · F O R · T A T A R     !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! See http://wiki.apertium.org/wiki/Turkic_lexicon

Multichar_Symbols
! Followed http://wiki.apertium.org/wiki/Turkic_languages

! Part of speech categories
%<n%>          ! Noun                       ! Исем              ! Существительное
%<np%>         ! Proper noun                ! Ялгызлык исем     ! Имя cобственное
%<adj%>        ! Adjective                  ! Сыйфат            ! Прилагательное
%<num%>        ! Numeral                    ! Сан               ! Числительное   
%<prn%>        ! Pronoun                    ! Алмашлык          ! Местоимение
%<det%>        ! Determiner                 ! Детерминатив      ! Детерминатив
%<v%>          ! Verb                       ! Фигыль            ! Глагол
%<vaux%>       ! Auxilary verb              ! Ярдәмче фигыль    ! Вспомогательный гл.
%<adv%>        ! Adverb                     ! Рәвеш             ! Наречие
%<post%>       ! Postposition               ! Бәйлек            ! Послелог
%<postadv%>    ! Postadverb                 ! "Арткы рәвеш"(*1) ! "Посленаречие"     !# "гына"
%<cnjcoo%>     ! Co-ordinating conjunction  ! Тезүче теркәгеч   ! Сочинительный союз
%<cnjsub%>     ! Sub-ordinating conjunction ! Ияртүче теркәгеч  ! Подчинительный союз
%<cnjadv%>     ! Adverbial conjunction      !                   ! ~Союз 
%<part%>       ! Particle                   ! Кисәкчә           ! Частица
%<ij%>         ! Interjection               ! Ымлык             ! Междометие
%<abbr%>       ! Abbreviation               ! Кыскартылма       ! Аббревиатура 
%<cop%>        ! Copula                     ! Копула            ! Связка
!%<ideo%>       ! Ideophone                  ! Аваз ияртеме      ! (Звуко)подражательное слово
!модаль сүз     ! модальное слово 

! *1. Ачыклаган сүзләренең артыннан килә торган рәвешләр/"кисәкчәләр" !# гына, ук

! Proper noun types
%<top%>            ! Toponym      ! Географик атама
%<ant%>            ! Anthroponym  ! Кеше исеме
%<cog%>            ! Cognomen     ! Фамилия
%<org%>            ! Organization ! Оешма исеме
%<al%>             ! Other        ! Калганнар

!! gender of anthroponyms and cognoms
%<m%>              ! Masculine    ! Мужской род
%<f%>              ! Feminine     ! Женский род
%<mf%>             ! Masculine/feminine !# basically cognoms without -ов/-ова ending

! "Syntactic" tags. Attributive use of non-adjectives etc.
%<attr%>           ! Attributive
%<subst%>          ! Substantive
%<advl%>           ! Adverbial

! Number ! Сан ! Число
%<sg%>             ! Singular  ! Берлек сан  ! Единственное число
%<pl%>             ! Plural    ! Күплек сан  ! Множественное число

! Possessives ! Тартым ! Притяжательные окончания
%<px1sg%>          ! First person singular
%<px2sg%>          ! Second person singular
%<px3sp%>          ! Third person singular/plural
%<px1pl%>          ! First person plural
%<px2pl%>          ! Second person singular
%<px%>             ! General possessive           !# н{I}к{I} (-ныкы/-неке)

! Cases ! Килешләр ! Падежи
%<nom%>            ! Баш килеш             ! Именительный
%<gen%>            ! Иялек килеше          ! Родительный
%<dat%>            ! Юнәлеш килеше         ! Дательный
%<acc%>            ! Төшем килеше          ! Винительый
%<abl%>            ! Чыгыш килеше          ! Творительный
%<loc%>            ! Урын-вакыт килеше     ! Местно-временнной

!! some additional ~cases
%<locattr%>    ! Relativised locative=Locative attribute=Местно-временной2 !# DAGI !! Not needed anymore, bcs we switched to <loc><attr>
%<sim%>        ! Similative !# DAй (-дай/-дәй, -тай, -тәй)
!лишительный   !# -сыз/-сез !! not used after posessives
!обладательный !# -лы/-ле   !! not used after posessives

! Levels of comparison of adjectives ! Сыйфат дәрәҗәләре ! Степени сравнения прилагательных
%<comp%>           ! Comparative  ! Чагыштыру дәрәҗәсе
%<sup%>            ! Superlative  ! Артыклык дәрәҗәсе
!! "weakening" form is not regular. So "weakened" :) adj are added as base lemmas.
!! Very few adj have this form. E.g. яшел-яшькелт, сары-саргылт [but] ак-аксыл, кара-карасу.
!! As a sublexicon maybe, so that they don't get <sup> ending, which is a nonsense

! Pronoun types ! Алмашлык төрләре ! Виды местоимений
%<pers%>           ! Personal
%<ref%>            ! Reflexive
! Pronoun and determiner types
%<dem%>            ! Demonstrative
%<ind%>            ! Indefinite
%<itg%>            ! Interrogative
%<qnt%>            ! Quantifier
%<neg%>            ! Negative
%<neg%>            ! Negative    ! FIXME:NOTE: also used for verbs

! Numeral types
%<ord%>            ! Ordinal
%<coll%>           ! Collective  !# берәү, икәү
%<dist%>           ! Distibutive !# берәр, икешәр

! Tense, aspect, mood
%<imp%>            ! Imperative
%<cond%>           ! Conditional  ! Шарт  ! Условное  !! -с{A}
%<caus%>           ! Causative
%<pass%>           ! Passive
%<coop%>           ! Cooperative

%<pres%>           ! -{A}
%<past%>           ! -{G}{A}н 
%<fut%>            ! -{I}р
%<ifi%>            ! -{D}{I} 

! Non-Finite verb forms ! WARNING: Number part of the tags is subject to change :)
                        ! Stay up-to-date

!! Participles
%<prc_impf%>       ! -{E}   !# ул _уйный_ алмады; мин _килә_ алмадым
%<prc2%>           ! -{I}п  !# "_Йоклап_ яткан мәче авызына тычкан үзе _килеп_ керми."
%<prc_vol%>        ! volition participle <pres><p3><sg>-с{I}-volition_endings  !# укый-сы-м килә, укый-сы-быз килә

!! Verbal adverbs - Хәл фигыльләр - Глаголные наречия 
%<gna_cond%>       ! -с{A}  !# "...кайда икәнен _белсә_ дә, әйтми..."
%<gna9%>           ! -{I}п  !# "...ул вакытта инде кояш _баеп_, йолдызлар күренә башлаган иде..." (Ф.Хөсни)

!! Verbal adjectives - Сыйфат фигыльләр - Глаголные причастия
%<gpr1%>           ! -{A} ! FIXME: for future use, verbal adjective

!! Gerunds - Исем фигыльләр
%<ger%>            ! -{U}
%<ger1%>           ! -м{A}{K}
%<ger3%>           ! -{G}{A}ч
%<ger4%>           ! -{G}{A}нч{I}
%<ger5%>           ! -{G}{A}н
%<ger6%>           ! -{I}р / -{A}р
%<ger7%>           ! -{A}ч{A}к
%<inf%>            ! Infinitive! Инфинитив ! -{A/I}рг{A}

!!! the next will be removed soon; kept to let others know about changes
%<ger2%>           ! -{I}п !WARNING: This was replaced with prc2 and gna9; turned off 
%<vol%>            ! !WARNING: This was replaced with prc_vol
%<ger8%>           ! -{E}с !WARNING: This was replaced with prc_vol

! Transitivity ! Переходность
%<tv%>             ! Transitive
%<iv%>             ! Intransitive

! Person ! Зат ! Лицо
%<p1%>             ! First person
%<p2%>             ! Second person
%<p3%>             ! Third person
%<frm%>            ! Formality FIXME: Check

! Clitics
%<qst%>            ! Modal question particle
%<mod_foc%>        ! focus thingy
%<mod_ass%>        ! assertive

! Punctuaton ! Пунктуация
%<sent%>           ! Sentence marker
%<guio%>           ! Hyphen
%<cm%>             ! Comma
%<apos%>           ! Apostrophe
%<rquot%>          ! Quote marker (right hand side) 
%<lquot%>          ! Quote marker (left hand side) 
%<rpar%>           ! Parenthetical marker (right hand side) 
%<lpar%>           ! Parenthetical marker (left hand side) 

%{L%}              ! Realised as
%{G%}              ! Realised as
%{D%}              ! Realised as
%{K%}              ! Realised as
%{I%}              ! Realised as 'ы', 'е'
%{A%}              ! Realised as 'a', 'ә' 
%{E%}              ! Realised as

%{s%}              ! Realised as 'с' in <px3> after vowel, 0 otherwise
%{n%}              ! Realised as 'н' in <px3> before certain cases, 0 otherwise
%{j%}              ! Realised as (maybe not needed; let's see)
%{w%}              ! Realised as
%{l%}              ! Realised as
%{U%}              ! Realised as у, ү, в
%{ъ%}              ! disappears and makes back harmony
%{д%}              ! Realised as д before vowels, 0 otherwise
%{т%}              ! Realised as т before vowels, 0 otherwise
%{е%}              ! Realised as е/ы in some contexts, 0 otherwise

%>                 ! Morpheme boundary

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Particles ; 
Copula ; 
Conjunctions ; 
Postpositions ; 
Postadverbs ;
Pronouns ; 
Determiners ; 
Numerals ; 
Nouns ; 
ProperNouns ;
Adjectives ; 
Adverbs ; 
Verbs ; 
Interjections ; 
Abbreviations ; 
Punctuation ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON SUBST

N-INFL-COMMON ; 
%<pl%>:%>%{L%}%{A%}р N-INFL-COMMON ;

! NOTE: Some substantivized things might not have pl. form, numerals e.g. 
! (not too sure, because in Tatar they have it: 
! #1 <11 ones meaning pupils from a given class: # өчләр = "pupils from the 3. class" 
! #2 "Мин белгәндә аңа, мөгаен, _иллеләр_ тирәсе булгандыр."
!
! If they don't have pl. form; link them to N-INFL-COMMON; if they do - to SUBST.
!                                                                     /I.S./

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!     CLITICS

LEXICON CLIT-OTHER            !FIXME:DISCUSS: make from this a separate clitics class for verbs

%+чы%<mod_foc%>:%>ч%{I%} # ;
# ;

LEXICON CLIT

%+да%<cnjcoo%>:% %{D%}%{A%} # ;
%+мы%<qst%>:%>м%{I%} # ;
%+дыр%<part%>:%>%{D%}%{I%}р # ;
!%+сы%<part%>:%>с%{I%} # ;    ! FIXME: check
CLIT-OTHER ;
# ;

!!!!!!!!!!!!!!!!!!!!!!     NOMINAL INFLECTION (N-INFL-COMMON)

! The following combinations are possible:
!
! stem > case                                        : әти<nom> : әтине<acc>
!
! stem > poss-real > case                            : әтием<px1sg><nom> : әтиемне<px1sg><acc>
! stem > poss-real > ныкы/дагы/дай(*1)               : әтиемнеке/китабындагы,өегездәге/әтиемдәй
! stem > poss-real > ныкы/дагы > case                : Монда _эчеңдәгене_ тышка чыгарырга ярамый. (источник: musulman.su) 
! stem > poss-real > [ныкы]/дагы > poss-real > case  : _Эчендәгесе_(*3) – тышында, диләр андый кешеләр турында. (источник: beznen.ru)
!
! stem > ныкы/дагы/дай                               : әтинеке/әтидәге/әтидәй
! stem > ныкы/дагы(*2) > case                        : Бу соңгы _еллардагының_ өчтән икесе чамасы.
!
! *1. "ныкы" form in some Tatar grammars is called "predicative genitive".
!     Indeed, when not substantivized, it has predicative use:
!     "Бу яулык әнинеке." (vs "Әнинең яулыгы.")
! *2. "дай" is considered to be an adverbializer, so no case endings after it (=no <subst> reading).
! *3. This doesn't work atm (it's recursive), but it doesn't seem to be very frequent also;
!     Most likely it is restricted to px3sp.
 
LEXICON CASE 

%<nom%>: CLIT ;               ! - - -
%<gen%>:%>н%{I%}ң CLIT ;      ! -ның/-нең
%<dat%>:%>%{G%}%{A%} CLIT ;   ! -га/-гә, -ка/-кә    ! -а/-ә in nouns with possessives in singular (see below)
%<acc%>:%>н%{I%} CLIT ;	      ! -ны/-не
%<abl%>:%>%{D%}%{A%}н CLIT ;  ! -дан/-дән, -тан/-тән, -нан/нән 
%<loc%>:%>%{D%}%{A%} CLIT ;   ! -да/-дә, -та/-тә

!! китап
!! китапның
!! китапка
!! китапны
!! китаптан
!! китапта

LEXICON ADJ-DAGI

%<subst%>: CASE ; 
%<attr%>: CLIT ;

LEXICON GENPOSS-ETC

%<px%>:%>н%{I%}к%{I%}%{n%} CASE ;        ! -ныкы/-неке
%<loc%>:%>%{D%}%{A%}г%{I%} ADJ-DAGI ;    ! -дагы/-дәге, -тагы/-тәге
%<sim%>:%>%{D%}%{A%}й CLIT ;             ! -дай/-дәй, -тай/-тәй       

LEXICON CASE-ETC
CASE ;
GENPOSS-ETC ;
  
LEXICON POSS-REAL

%<px1sg%>:%>%{I%}м CASE-ETC ;                 ! -ым/-ем, -м
%<px2sg%>:%>%{I%}ң CASE-ETC ;                 ! -ың/-ең, -ң
%<px3sp%>:%>%{s%}%{I%}%{n%} CASE-ETC ;        ! -ы/е, -сы/-се
%<px1pl%>:%>%{I%}б%{I%}з CASE-ETC ;           ! -ыбыз/-ебез, -быз/-без
%<px2pl%>:%>%{I%}%{G%}%{I%}з CASE-ETC ;       ! -ыгыз/-егез, -гыз/-гез

CASE-ETC ; ! FIXME:NOTE:! this was temporarily put here because some gerunds and pronouns are linked
                        ! to POSS-REAL. They should be linked to SUBST actually (and they will) /I.S./ 

! In dative nouns with <px1sg> <px2sg> and <px3sp> gets an "-a" ending instead of "-га/-гә, -ка/-кә"
! In nouns with <px3sp> there is an enclitic "н" in most cases  
!! китабым      китабың	        китабы
!! китабымның   китабыңның      китабының
!! китабыма!    китабыңа!       китабы-н-а!     
!! китабымны    китабыңны       китабы-н      ! китабы-н-ы is an old form. Only in poetry :)
!! китабымнан   китабыңнан      китабы-н-нан
!! китабымда    китабыңда       китабы-н-да

LEXICON N-INFL-COMMON 
POSS-REAL ;
CASE-ETC ;

!!!!!!!!!!!!!!!!!!!!!!     PART-OF-SPEECH LEXICONS (beginning)

LEXICON NLEX

%<n%>%<attr%>: # ; 
%<n%>: SUBST ;

LEXICON DIGITLEX

%<num%>: # ; 

LEXICON NUMLEX

%<num%>: # ; 
%<num%>%<subst%>: SUBST ; 
%<num%>%<ord%>:%>%{I%}нч%{I%} SUBST ; 
%<num%>%<dist%>:%>%{A%}р SUBST ; 

LEXICON NUM-COLL

%<num%>%<coll%>: # ;

LEXICON NPCOGMLEX

%<np%>%<cog%>%<m%>: CASE ; 

LEXICON NPCOGFLEX

%<np%>%<cog%>%<f%>: CASE ; 

LEXICON NPCOGMFLEX

%<np%>%<cog%>%<mf%>: CASE ; 

LEXICON NPANTMLEX

%<np%>%<ant%>%<m%>: CASE ; 

LEXICON NPANTFLEX

%<np%>%<ant%>%<f%>: CASE ; 

LEXICON NPTOPLEX

%<np%>%<top%>: CASE ; 

!!!!!!!!!!!!!!!!!!!!!!     ADJECTIVE'S (ADJECTIVIAL) INFLECTION (for verbal adjectives A2 should be used)
!                                                               (a separate ADJ-COMMON might be even better, see kaz.lexc) 
! FIXME: this section needs refactoring
!! e.g. change <adj><comp><subst> to <adj><subst><comp>
!! and make use of ADJ-LEVELS class

LEXICON ADJ-LEVELS

%<comp%>:%>р%{A%}%{K%} CLIT ;
CLIT ;

LEXICON A1                 ! adjectives that can be both substantivized and andverbialized #! яхшы

%<adj%>: CLIT ; 
%<adj%>%<comp%>:%>р%{A%}%{K%} CLIT ; 

%<adj%>%<subst%>: SUBST ;                             !# "Яхшы яхшыны тудыра." 
!%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} SUBST ;  !# яхшыракны таптым

%<adj%>%<advl%>: CLIT ;                               !# "Тиз янган тиз сүнгән."
!%<adj%>%<comp%>%<advl%>:%>%{I%}р%{A%}%{K%} CLIT ;    !# "Бәла бәхеттән тизрәк йөри."

LEXICON A2                 ! (?derived/not fully lexicalized) adjectives without adverbial reading #! язгы

%<adj%>: CLIT ;
!%<adj%>%<comp%>:%>%{I%}р%{A%}%{K%} CLIT ; ! they also seem not to take comp. level !# *язгырак

%<adj%>%<subst%>: SUBST ;                             !# "Ул 14-25 яшьлекләргә бирелә." 
!%<adj%>%<comp%>%<subst%>:%>%{I%}р%{A%}%{K%} SUBST ;

LEXICON A3                 ! adjectives with (only) predicative use     !# бар, юк 
                           ! FIXME:NOTE: Maybe this class is not really needed, because it turned out :) that
                           ! even "бар" and "юк" can take case endings: !# "Хатыны юкның якыны юк."
                                                                        !# "Эше барның ашы бар."   
%<adj%>: CLIT ;

!!!!!!!!!!!!!!!!!!!!!!     VERBAL INFLECTION

LEXICON V-PERS-S1 

%<p1%>%<sg%>:%>м CLIT ;
%<p2%>%<sg%>:%>с%{I%}ң CLIT ;
%<p2%>%<frm%>%<sg%>:%>с%{I%}з CLIT ; ! FIXME: Check 'Ни өчен беләсезме?' ! <frm><sg> and <frm><pl> are the same forms in Tatar; so maybe <frm><sp> would be better /I.S./
%<p3%>%<sg%>:%> CLIT ;
%<p1%>%<pl%>:%>б%{I%}з CLIT ;
%<p2%>%<pl%>:%>г%{I%}з CLIT ;
%<p3%>%<pl%>:%>%{L%}%{A%}р CLIT ;

LEXICON V-PERS-S2
!! кыска төрләнеш
%<p1%>%<sg%>:%>м CLIT ;
%<p2%>%<sg%>:%>ң CLIT ;
%<p3%>%<sg%>:%> CLIT ;
%<p1%>%<pl%>:%>к CLIT ;
%<p2%>%<pl%>:%>г%{I%}з CLIT ;
%<p3%>%<pl%>:%>%{L%}%{A%}р CLIT ;

LEXICON V-PERS-S3-NO1SG

%<p2%>%<sg%>:%>с%{I%}ң CLIT ;
%<p3%>%<sg%>:%> CLIT ;
%<p1%>%<pl%>:%>б%{I%}з CLIT ;
%<p2%>%<pl%>:%>с%{I%}г%{I%}з CLIT ;
%<p3%>%<pl%>:%>%{L%}%{A%}р CLIT ;

LEXICON V-PERS-S3

%<p1%>%<sg%>:%>м%{I%}н CLIT ;
V-PERS-S3-NO1SG ; 

LEXICON V-PERS-S4 ! Volition endings

%<p1%>%<sg%>:м CLIT ;
%<p2%>%<sg%>:ң CLIT ;
%<p3%>%<sg%>: CLIT ;
%<p1%>%<pl%>:б%{I%}з CLIT ;
%<p2%>%<pl%>:г%{I%}з CLIT ;
%<p3%>%<pl%>:%{L%}%{A%}р%{I%} CLIT ;

!LEXICON V-PERS-S4 ! Volition endings

!%<p1%>%<sg%>:%>м%{I%}н CLIT ;
!%<p2%>%<sg%>:%>с%{I%}ң CLIT ;
!%<p3%>%<sg%>:%> CLIT ;
!%<p1%>%<pl%>:%>б%{I%}з CLIT ;
!%<p2%>%<pl%>:%>с%{I%}г%{I%}з CLIT ;
!%<p3%>%<pl%>:%>%{L%}%{A%}р CLIT ;

LEXICON V-FUT-IR
!! verbs that get -рга/-ргә ending should be also put here,
!! since in twol there is a rule deleting %{I%} after vowels
!! e.g. уйна-рга

%<fut%>:%>%{I%}р V-PERS-S3 ; 
%<neg%>%<fut%>:%>м%{A%}с V-PERS-S3-NO1SG ; 
%<neg%>%<fut%>%<p1%>%<sg%>:%>м%{A%}%>м CLIT ; 
%<neg%>%<fut%>%<p1%>%<sg%>:%>м%{A%}с%>м%{I%}н CLIT ; ! Dir/LR 
%<ger6%>:%>%{I%}р POSS-REAL ;
%<inf%>:%>%{I%}рг%{A%} # ;
%<neg%>%<ger6%>:%>м%{A%}%>%{I%}р POSS-REAL ;

LEXICON V-FUT-AR

%<fut%>:%>%{A%}р V-PERS-S3 ; 
%<neg%>%<fut%>:%>м%{A%}с V-PERS-S3 ; 
%<neg%>%<fut%>%<p1%>%<sg%>:%>м%{A%}%>м CLIT ; 
%<neg%>%<fut%>%<p1%>%<sg%>:%>м%{A%}с%>м%{I%}н CLIT ; ! Dir/LR 
%<ger6%>:%>%{A%}р POSS-REAL ;
%<inf%>:%>%{A%}рг%{A%} # ;
%<neg%>%<ger6%>:%>м%{A%}%>%{A%}р POSS-REAL ;

LEXICON V-FINITE-FORMS

%<pres%>:%>%{E%} V-PERS-S1 ; 
%<ifi%>:%>%{D%}%{I%} V-PERS-S2 ; 
%<past%>:%>%{G%}%{A%}н V-PERS-S3 ; 

LEXICON V-COMMON-LEX

V-FINITE-FORMS ; 

! Participles
%<prc2%>:%>%{I%}п # ; 
%<prc_impf%>:%>%{E%} # ; 
%<prc_vol%>:%>%{E%}с%{I%} V-PERS-S4 ; 

! Gerunds
%<ger%>:%>%{U%} POSS-REAL ;
%<ger1%>:%>м%{A%}%{K%} POSS-REAL ; 
%<ger3%>:%>%{G%}%{A%}ч SUBST ; 
%<ger4%>:%>%{G%}%{A%}нч%{I%} POSS-REAL ; 
%<ger5%>:%>%{G%}%{A%}н POSS-REAL ;
%<ger7%>:%>%{A%}ч%{A%}к POSS-REAL ;
!! TO REMOVE
!%<ger2%>:%>%{I%}п POSS-REAL ; 
!%<ger8%>:%>%{E%}с V-PERS-S4 ;

! Verbal adverbs
%<gna_cond%>:%>с%{A%} V-PERS-S2 ;   !# "...кайда икәнен _белсә_ дә, әйтәсе килми..."
%<gna9%>:%>%{I%}п CLIT ;

%<cond%>:%>с%{A%} V-PERS-S2 ;

%<imp%>%<p2%>%<sg%>: CLIT ;
%<imp%>%<p2%>%<pl%>:%{I%}г%{I%}з CLIT ;

LEXICON V-COMMON-PASSIVE

%<v%>%<tv%>%<pass%>:%>%{I%}%{l%} V-COMMON-LEX ; 
%<v%>%<tv%>%<pass%>:%>%{I%}%{l%} V-FUT-IR ; 

LEXICON V-NEG
%<neg%>:%>м%{A%} V-COMMON-LEX ;
%<neg%>%<prc2%>:%>м%{A%}%>%{E%}%>ч%{A%} CLIT ; ! FIXME: Check continuations ! '#' was changed to 'CLIT' /I.S./
%<neg%>%<gna9%>:%>м%{A%}%>%{E%}%>ч%{A%} CLIT ; ! FIXME: Check continuations ! '#' was changed to 'CLIT' /I.S./
V-COMMON-LEX ; 

LEXICON V-DER

%<caus%>:%>%{I%}т V-NEG ;
%<coop%>:%>%{I%}ш V-NEG ;
V-NEG ;

LEXICON V-IV-IR-LEX

%<v%>%<iv%>: V-COMMON-LEX ; 
%<v%>%<iv%>: V-FUT-IR; 
%<v%>%<iv%>: V-DER; 

LEXICON V-TV-NOPASS-IR-LEX

%<v%>%<tv%>: V-COMMON-LEX ; 
%<v%>%<tv%>: V-FUT-IR; 
%<v%>%<tv%>: V-DER; 

LEXICON V-TV-PASS-IR-LEX

%<v%>%<tv%>%<pass%>: V-COMMON-LEX ; 
%<v%>%<tv%>%<pass%>: V-FUT-IR; 
%<v%>%<tv%>%<pass%>: V-DER; 

LEXICON V-TV-IR-LEX

%<v%>%<tv%>: V-COMMON-LEX ; 
%<v%>%<tv%>: V-FUT-IR; 
%<v%>%<tv%>: V-DER; 
V-COMMON-PASSIVE ;

LEXICON V-IV-AR-LEX

%<v%>%<iv%>: V-COMMON-LEX ; 
%<v%>%<iv%>: V-FUT-AR ;
%<v%>%<iv%>: V-DER; 

LEXICON V-TV-AR-LEX

%<v%>%<tv%>: V-COMMON-LEX ; 
%<v%>%<tv%>: V-FUT-AR ;
%<v%>%<tv%>: V-DER; 
V-COMMON-PASSIVE ;

!!!!!!!!!!!!!!!!!!!!!!     auxiliary verbs

LEXICON VAUX

%<vaux%>: V-COMMON-LEX ;
%<vaux%>%<neg%>:%>м%{A%} V-COMMON-LEX ;

!!!!!!!!!!!!!!!!!!!!!!     copula

LEXICON COP-I

и%<cop%>%<neg%>:түгел V-PERS-S3 ;
и%<cop%>%<ger5%>:%>икән POSS-REAL ;
и%<cop%>%<ifi%>:%>иде V-PERS-S2 ;        

!!!!!!!!!!!!!!!!!!!!!!     PRONOUN'S INFLECTION

LEXICON PRN-PERS

! FIXME:NOTE: some of these are regular and can eventually be pointed
! to the CASE cont.class; this would make it more concise (but less readable)    

мин%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:мин # ; 
мин%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:минем # ; 
мин%<prn%>%<pers%>%<p1%>%<sg%>%<dat%>:миңа # ; 
мин%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:мине # ; 
мин%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:миндә # ; 
мин%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:миннән # ; 
!мин%<prn%>%<pers%>%<p1%>%<sg%>%<px%>:минеке # ; 
!мин%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>%<attr%>:миндәге # ; 

син%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:син # ; 
син%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:синең # ; 
син%<prn%>%<pers%>%<p2%>%<sg%>%<dat%>:сиңа # ; 
син%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:сине # ; 
син%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:синдә # ; 
син%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:синнән # ; 
!син%<prn%>%<pers%>%<p2%>%<sg%>%<px%> : синеке 
!син%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>%<attr%> : синдәге 

ул%<prn%>%<pers%>%<p3%>%<sg%>%<nom%>:ул # ; 
ул%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:аның # ; 
ул%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:аңа # ;             !! also synonymic variants "аңар" and "аңарга" (only analysed)
ул%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:аңар # ; Dir/LR
ул%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:аңарга # ; Dir/LR
ул%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:аны # ; 
ул%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:анда # ;            !! also: "аңарда" (only analysed)  !!"анда" "аннан" usually instead of inanimate nouns 
ул%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:аңарда # ; Dir/LR
ул%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:аннан # ;           !! also: "аңардан" (only analysed) !!"аңарда" "аңардан" -- instead of person
ул%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:аңардан # ; Dir/LR

без%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:без # ; 
без%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:безнең # ; 
без%<prn%>%<pers%>%<p1%>%<pl%>%<dat%>:безгә # ; 
без%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:безне # ; 
без%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:бездә # ; 
без%<prn%>%<pers%>%<p1%>%<pl%>%<abl%>:бездән # ; 

сез%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:сез # ; 
сез%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:сезнең # ; 
сез%<prn%>%<pers%>%<p2%>%<pl%>%<dat%>:сезгә # ; 
сез%<prn%>%<pers%>%<p2%>%<pl%>%<acc%>:сезне # ; 
сез%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:сездә # ; 
сез%<prn%>%<pers%>%<p2%>%<pl%>%<abl%>:сездән # ; 

алар%<prn%>%<pers%>%<p3%>%<pl%>%<nom%>:алар # ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>%<gen%>:аларның # ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>%<dat%>:аларга # ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>%<acc%>:аларны # ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>%<loc%>:аларда # ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>%<abl%>:алардан # ; 

LEXICON PRNDEM-BU

%<prn%>%<dem%>%<nom%>:бу # ;           ! FIXME: these can get possessives too, e.g. монысының, монысына
%<prn%>%<dem%>%<gen%>:моның # ;
%<prn%>%<dem%>%<dat%>:моңа # ;
%<prn%>%<dem%>%<acc%>:моны # ;
%<prn%>%<dem%>%<loc%>:монда # ;
%<prn%>%<dem%>%<abl%>:моннан # ;
%<prn%>%<dem%>%<pl%>:бо%>%{L%}%{A%}р CASE ;

LEXICON PRNDEM-ŠUL

%<prn%>%<dem%>%<nom%>:шул # ;
%<prn%>%<dem%>%<gen%>:шуның # ;
%<prn%>%<dem%>%<dat%>:шуңа # ;
%<prn%>%<dem%>%<acc%>:шуны # ;
%<prn%>%<dem%>%<loc%>:шунда # ;
%<prn%>%<dem%>%<abl%>:шуннан # ;
%<prn%>%<dem%>%<pl%>:шул%>%{L%}%{A%}р CASE ;

LEXICON PRNDEM-UL                     ! FIXME: refactor this, link all remaining prn.dem's to one cont. class

%<prn%>%<dem%>%<nom%>:ул # ; 
%<prn%>%<dem%>%<gen%>:аның # ; 
%<prn%>%<dem%>%<dat%>:аңа # ;
%<prn%>%<dem%>%<acc%>:аны # ; 
%<prn%>%<dem%>%<loc%>:анда # ; 
%<prn%>%<dem%>%<abl%>:аннан # ;
%<prn%>%<dem%>%<pl%>:алар CASE ;

LEXICON PRNITGLEX

%<prn%>%<itg%>: POSS-REAL ; 

LEXICON PRNDEMLEX

%<prn%>%<dem%>: CASE ; 
%<prn%>%<dem%>%<pl%>:%>%{L%}%{A%}р CASE ; 

LEXICON PRNNEG

%<prn%>%<neg%>: CASE ; 

LEXICON PRNREFLEX

%<prn%>%<ref%>: POSS-REAL ; 

!!!!!!!!!!!!!!!!!!!!!!     PART-OF-SPEECH LEXICONS (end)

LEXICON DETDEMLEX

%<det%>%<dem%>: # ; 

LEXICON DETQNTLEX 

%<det%>%<qnt%>: # ; 

LEXICON DETINDLEX

%<det%>%<ind%>: # ; 

LEXICON DETITGLEX

%<det%>%<itg%>: # ; 

LEXICON POSTLEX

%<post%>: # ; 

LEXICON POSTADVLEX

%<postadv%>: # ; 

LEXICON ADVITGLEX

%<adv%>%<itg%>: # ; 

LEXICON ADVLEX

%<adv%>: CLIT ; 

LEXICON CCLEX

%<cnjcoo%>: # ; 

LEXICON CALEX

%<cnjadv%>: # ; 

LEXICON QSTLEX

%<qst%>: # ; 

LEXICON INTERJ

%<ij%>: # ;

LEXICON ABBR 

%<abbr%>: # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Particles

мы:мы QSTLEX ; ! ""
!CLIT ;
!CLIT-OTHER ;

LEXICON Copula

!и:и COP-I ; ! ""
COP-I ; ! ""

LEXICON Conjunctions

я:я CCLEX ; ! ""
яисә:яисә CCLEX ; ! ""
һәм:һәм CCLEX ; ! ""
әмма:әмма CCLEX ; ! ""
да:да CCLEX ; ! "also" Dir/RL
да:да CCLEX ; ! "" Dir/LR
да:та CCLEX ; ! "" Dir/LR
да:дә CCLEX ; ! "" Dir/LR
да:тә CCLEX ; ! "" Dir/LR
шул% сәбәпле:шул% сәбәпле CALEX ; ! ""

LEXICON Postpositions 

астына:астына POSTLEX ; ! ""
белән:белән POSTLEX ; ! ""
карамастан:карамастан POSTLEX ; ! ""

LEXICON Postadverbs

гына:%~гына POSTADVLEX ; ! "only" Dir/RL
гына:гына POSTADVLEX ; ! ""       Dir/LR
гына:генә POSTADVLEX ; ! ""       Dir/LR
гына:кына POSTADVLEX ; ! ""       Dir/LR
гына:кенә POSTADVLEX ; ! ""       Dir/LR

да:%~да POSTADVLEX ; ! ""     Dir/RL
да:да POSTADVLEX ; ! ""       Dir/LR
да:дә POSTADVLEX ; ! ""       Dir/LR
да:та POSTADVLEX ; ! ""       Dir/LR
да:тә POSTADVLEX ; ! ""       Dir/LR

карамастан:карамастан POSTADVLEX ; ! "in spite of"

LEXICON Pronouns

PRN-PERS ; 

ни:ни PRNITGLEX ; ! ""
нимә:нимә PRNITGLEX ; ! ""
нәрсә:нәрсә PRNITGLEX ; ! "what"
кем:кем PRNITGLEX ; ! ""

бу: PRNDEM-BU ; ! ""
шул: PRNDEM-ŠUL ; ! ""
ул: PRNDEM-UL ; ! ""
шушы:шушы PRNDEMLEX ; ! ""     !FIXME: these share the same endings with "бу", see мо-ңа <> шушы-ңа
теге:теге PRNDEMLEX ; ! ""     !теге-ңә (exceptional case endings in singular)
                               !TODO:maybe it is possible to link all PRNDEMs to one Continuation class?

һичнәрсә:һичнәрсә PRNNEG ; ! ""
бернәрсә:бернәрсә PRNNEG ; ! ""

үз:үз PRNREFLEX ; ! ""

LEXICON Determiners

бу:бу DETDEMLEX ; ! ""
теге:теге DETDEMLEX ; ! ""
шул:шул DETDEMLEX ; ! ""
шушы:шушы DETDEMLEX ; ! ""

бар:бар DETQNTLEX ; ! ""
барлык:барлык DETQNTLEX ; ! ""
бөтен:бөтен DETQNTLEX ; ! ""

башка:башка DETINDLEX ; ! "other"
бер:бер DETINDLEX ; ! ""
бернинди:бернинди DETINDLEX ; ! ""
берәр:берәр DETINDLEX ; ! "some" ! FIXME: JNW to check this
бүтән:бүтән DETINDLEX ; ! "other"
һичбер:һичбер DETINDLEX ; ! ""

кай:кай DETITGLEX ; ! "which"

LEXICON Numerals 

нуль:нуль NUMLEX ; ! ""
нуль:ноль NUMLEX ; ! "" ! Dir/LR
нуль:нол NUMLEX ; ! "" ! Dir/LR
бер:бер NUMLEX ; ! ""
ике:ике NUMLEX ; ! ""
өч:өч NUMLEX ; ! ""
дүрт:дүрт NUMLEX ; ! ""
биш:биш NUMLEX ; ! ""
алты:алты NUMLEX ; ! ""
җиде:җиде NUMLEX ; ! ""
сигез:сигез NUMLEX ; ! ""
тугыз:тугыз NUMLEX ; ! ""
ун:ун NUMLEX ; ! ""

унбер:унбер NUMLEX ; ! ""
унике:унике NUMLEX ; ! ""
унөч:унөч NUMLEX ; ! ""
ундүрт:ундүрт NUMLEX ; ! ""
унбиш:унбиш NUMLEX ; ! ""
уналты:уналты NUMLEX ; ! ""
унҗиде:унҗиде NUMLEX ; ! ""
унсигез:унсигез NUMLEX ; ! ""
унтугыз:унтугыз NUMLEX ; ! ""

егерме:егерме NUMLEX ; ! ""
утыз:утыз NUMLEX ; ! ""
кырык:кырык NUMLEX ; ! ""
илле:илле NUMLEX ; ! ""
алтмыш:алтмыш NUMLEX ; ! ""
җитмеш:җитмеш NUMLEX ; ! ""
сиксән:сиксән NUMLEX ; ! ""
туксан:туксан NUMLEX ; ! ""
йөз:йөз NUMLEX ; ! ""

мең:мең NUMLEX ; ! ""
төмән:төмән NUMLEX ; ! "archaic for 10 000"
түмән:түмән NUMLEX ; ! Dir/LR !"archaic for 10 000"
миллион:миллион NUMLEX ; ! ""
миллиард:миллиард NUMLEX ; ! ""

биш:бишәү NUM-COLL ;           
ике:икәү NUM-COLL ;
өч:өчәү NUM-COLL ;
дүрт:дүртәү NUM-COLL ;
биш:бишәү NUM-COLL ;

! Technically this goes on to higher numbers  !! FIXME:DISCUSS: maybe we'll start derive them from the basic numerals
! but its use beyond 5(?) is rare
! and is of questionable grammaticality

LEXICON Nouns 

агач:агач NLEX ; ! "" 
алд:ал%{д%} NLEX ; ! ""
ана:ана NLEX ; ! "" 
арка:арка NLEX ; ! "" 
бакча:бакча NLEX ; ! ""
бакча:бакча NLEX ; ! ""
бала:бала NLEX ; ! "" 
баш:баш NLEX ; ! ""
баш:баш NLEX ; ! ""
баш:баш NLEX ; ! "" 
вакыт:вакыт NLEX ; ! ""
вакыт:вакыт NLEX ; ! ""
вакыт:вакыт NLEX ; ! "" 
вакыт:вакыт NLEX ; ! "" 
заман:заман NLEX ; ! "" 
зат:зат NLEX ; ! ""
кеше:кеше NLEX ; ! ""
кеше:кеше NLEX ; ! "" 
кош:кош NLEX ; ! ""
кош:кош NLEX ; ! ""
кул:кул NLEX ; ! "" 
кыз:кыз NLEX ; ! ""
кыз:кыз NLEX ; ! "" 
күз:күз NLEX ; ! ""
күз:күз NLEX ; ! "" 
малай:малай NLEX ; ! ""
малай:малай NLEX ; ! "" 
мизгел:мизгел NLEX ; ! ""
мизгел:мизгел NLEX ; ! ""
мизгел:мизгел NLEX ; ! "" 
песи:песи NLEX ; ! "cat"
пиво:пиво NLEX ; ! ""
сеңел:сеңел NLEX ; ! "" ! Dir/LR
сеңел:сең%{е%}л%{е%} NLEX ; ! ""
сорау:сорау NLEX ; ! ""
су:су NLEX ; ! "" 
суык:суык NLEX ; ! ""
суык:суык NLEX ; ! ""
сыра:сыра NLEX ; ! ""
тавыш:тавыш NLEX ; ! ""
тирә:тирә NLEX ; ! ""
тыш:тыш NLEX ; ! ""
тәрәзә:тәрәзә NLEX ; ! "" 
уен:уен NLEX ; ! "" 
урындык:урындык NLEX ; ! ""
хайван:хайван NLEX ; ! ""
чиләк:чиләк NLEX ; ! ""
эт:эт NLEX ; ! "" 
эч:эч NLEX ; ! ""
эч:эч NLEX ; ! ""
эч:эч NLEX ; ! "" 
яшь:яшь NLEX ; ! ""
яшь:яшь NLEX ; ! "" 
җавап:җавап NLEX ; ! ""
җавап:җавап NLEX ; ! "" 
җир:җир NLEX ; ! "" 
җылы:җылы NLEX ; ! ""
җылы:җылы NLEX ; ! ""
һава:һава NLEX ; ! "" 
әни:әни NLEX ; ! "" 
өй:өй NLEX ; ! "" 
өстәл:өстәл NLEX ; ! ""
өстәл:өстәл NLEX ; ! ""

LEXICON ProperNouns

Әхмәт:Әхмәт NPANTMLEX ; ! ""
Гөлнара:Гөлнара NPANTFLEX ; ! ""

LEXICON Adjectives 

бар:бар A2 ; ! ""
юк:юк A2 ; ! ""
бәләкәй:бәләкәй A1 ; ! ""
бәләкәй:бәләкәй A1 ; ! ""
бәхетле:бәхетле A1 ; ! ""
бөек:бөек A1 ; ! ""
игътибарлы:игътибарлы A1 ; ! ""
иске:иске A1 ; ! ""
карт:карт A1 ; ! ""
кече:кече A1 ; ! ""
кече:кече A1 ; ! ""
кече:кече A1 ; ! ""
кече:кече A1 ; ! ""
кечек:кечек A1 ; ! ""
кечкенә:кечкенә A1 ; ! ""
олы:олы A1 ; ! ""
суык:суык A1 ; ! ""
тиеш:тиеш A2 ; ! ""
тиз:тиз A1 ; ! ""
тиз:тиз A1 ; ! ""
тиз:тиз A1 ; ! ""
тиз:тиз A1 ; ! ""
якын:якын A1 ; ! ""
яхшы:яхшы A1 ; ! ""
яхшы:яхшы A1 ; ! ""
әйбәт:әйбәт A1 ; ! ""
әйбәт:әйбәт A1 ; ! ""

LEXICON Adverbs 

кайда:кайда ADVITGLEX ; ! ""
кая:кая ADVITGLEX ; ! ""
аннары:аннары ADVLEX ; ! ""
бергә:бергә ADVLEX ; ! "together"
бик:бик ADVLEX ; ! ""
бүген:бүген ADVLEX ; ! ""
иртәгә:иртәгә ADVLEX ; ! "tomorrow"
кичә:кичә ADVLEX ; ! ""
тагын:тагын ADVLEX ; ! ""
тик:тик ADVLEX ; ! ""
шактый:шактый ADVLEX ; ! "довольно, заметно"
һаман:һаман ADVLEX ; ! ""
һәрвакыт:һәрвакыт ADVLEX ; ! ""
әлбәттә:әлбәттә ADVLEX ; ! ""
әкрен% генә:әкрен% генә ADVLEX ; ! "" ! Use/MT

LEXICON Verbs

ал:ал V-TV-IR-LEX ; ! ""
аша:аша V-TV-IR-LEX ; ! ""
бар:бар V-IV-IR-LEX ; ! ""
бел:бел V-TV-IR-LEX ; ! ""
бетер:бетер V-TV-IR-LEX ; ! "" ! FIXME: causative
бир:бир V-TV-IR-LEX ; ! ""
бул:бул V-IV-IR-LEX ; ! ""
ди:ди V-TV-IR-LEX ; ! ""
ишет:ишет V-TV-IR-LEX ; ! ""
йокла:йокла V-IV-IR-LEX ; ! ""
йөгер:йөгер V-IV-IR-LEX ; ! ""
биләмдә% йөр:биләмдә% йөр  V-IV-IR-LEX ; ! "gezinmek"
кара:кара V-TV-AR-LEX ; ! ""
кара:кара V-TV-IR-LEX ; ! ""
кара:кара V-TV-IR-LEX ; ! ""
кара:кара V-TV-IR-LEX ; ! ""
кара:кара V-TV-IR-LEX ; ! ""
каран:каран V-TV-IR-LEX ; ! ""
кил:кил V-IV-IR-LEX ; ! ""
кит:кит V-IV-AR-LEX ; ! ""
кит:кит V-IV-AR-LEX ; ! ""
кит:кит V-IV-AR-LEX ; ! ""
кит:кит V-IV-AR-LEX ; ! ""
кит:кит V-IV-AR-LEX ; ! ""
күзәт:күзәт V-TV-IR-LEX ; ! ""
күр:күр V-TV-NOPASS-IR-LEX ; ! ""
күр:күр V-TV-NOPASS-IR-LEX ; ! ""
күр:күрен V-TV-PASS-IR-LEX ; ! ""
күр:күрен V-TV-PASS-IR-LEX ; ! ""
көл:көл V-IV-IR-LEX ; ! ""
сайла:сайла V-TV-IR-LEX ; ! ""
сора:сора V-TV-IR-LEX ; ! ""
сора:сора V-TV-IR-LEX ; ! ""
сора:сора V-TV-IR-LEX ; ! ""
сөйләш:сөйләш V-IV-IR-LEX ; ! ""
тап:тап V-TV-AR-LEX ; ! ""
тап:тап V-TV-AR-LEX ; ! ""
тап:тап V-TV-AR-LEX ; ! ""
телә:телә V-TV-IR-LEX ; ! ""
тыңла:тыңла V-TV-IR-LEX ; ! ""
тыңла:тыңла V-TV-IR-LEX ; ! ""
уйла:уйла V-TV-IR-LEX ; ! ""
уйна:уйна V-TV-IR-LEX ; ! ""
уйна:уйна V-TV-IR-LEX ; ! ""
утыр:утыр V-IV-IR-LEX ; ! ""
эзлә:эзлә V-TV-IR-LEX ; ! ""
эзлә:эзлә V-TV-IR-LEX ; ! ""
эч:эч V-TV-AR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
эшлә:эшлә V-TV-IR-LEX ; ! ""
ярат:ярат V-TV-IR-LEX ; ! ""
ярат:ярат V-TV-IR-LEX ; ! ""
яшер:яшер V-TV-IR-LEX ; ! ""
әйт:әйт V-TV-IR-LEX ; ! ""
ал:ал VAUX ; ! ""
бетер:бетер VAUX ; ! "" ! FIXME:causative
бир:бир VAUX ; ! ""
бул:бул VAUX ; ! ""
кит:кит VAUX ; ! ""
кил:кил VAUX ; ! ""

LEXICON Interjections 

димәк:димәк INTERJ ; 
юк:юк INTERJ ; 
әйе:әйе INTERJ ; 
әлбәттә:әлбәттә INTERJ ; 

LEXICON Abbreviations

км:км ABBR ; 
км²:км² ABBR ; 
км³:км³ ABBR ; 

LEXICON Punctuation

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ; 
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;

<(%0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9)+>  DIGITLEX ; ! Use/Circ
