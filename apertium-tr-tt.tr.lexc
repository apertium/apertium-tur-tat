!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                             T U R K I S H                               !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>         ! Noun
%<v%>         ! Verb
%<prn%>       ! Pronoun
%<num%>       ! Numeral
%<adv%>       ! Adverb multichar
%<adj%>       ! Adjective
%<det%>       ! Determiner
%<np%>        ! Proper noun
%<ij%>        ! Interjection
%<post%>      ! Postposition
%<abbr%>      ! Abbreviation
%<cnjcoo%>    ! Co-ordinating conjunction
%<cnjsub%>    ! Sub-ordinating conjunction (basically 'ki')
%<cnjadv%>    ! Adverbial conjunction

%<pred%>      ! Predicate
%<subst%>     ! Substantive
%<attr%>      ! Attributive
%<advl%>      ! Adverbial

%<pers%>      ! Personal
%<ord%>       ! Ordinal
%<dem%>       ! Demonstrative
%<ind%>       ! Indefinite
%<itg%>       ! Interrogative
%<qnt%>       ! Quantifier
%<ref%>       ! Reflexive
%<neg%>       ! Negative
%<cop%>       ! Copula
%<rel%>       ! Relative

%<top%>       ! Toponym
%<al%>        ! Others (altres)
%<ant%>       ! Anthroponym
%<acr%>       ! Acronym
%<cog%>       ! Surname (cognomen)

%<m%>         ! Masculine
%<f%>         ! Feminine
%<mf%>        ! Masculine / feminine

%<p1%>        ! First person
%<p2%>        ! Second person
%<p3%>        ! Third person
%<frm%>       ! Formal

%<sg%>        ! Singular
%<pl%>        ! Plural

%<nom%>       ! Nominative (unmarked)
%<loc%>       ! Locative
%<gen%>       ! Genitive
%<acc%>       ! Accusative 
%<dat%>       ! Dative
%<abl%>       ! Ablative
%<ins%>       ! Instrumental


%<px1sg%>     ! First person singular possessive
%<px2sg%>     ! Second person singular possessive
%<px3sg%>     ! Third person singular possessive
%<px1pl%>     ! First person plural possessive
%<px2pl%>     ! Second person plural possessive
%<px3pl%>     ! Third person plural possessive

%<iv%>        ! Intransitive
%<tv%>        ! Transitive
%<pass%>      ! Passive
%<caus%>      ! Causative
%<abil%>      ! Abilitative

%<prc1%>      ! Participle #1: -{bY}{I}p

%<gna1%>      ! Verbal adverb #1: -{bY}{I}p
%<gna9%>      ! Verbal adverb #9: -{bY}{A}

%<ger1%>      ! Gerund #1: -m{A}
%<ger2%>      ! Gerund #2: -m{A}{K}
%<ger3%>      ! Gerund #3: -{D}{I}{k}
%<ger4%>      ! Gerund #4: -{bY}{I}ş
%<ger5%>      ! Gerund #5: -{bY}{A}n
%<ger6%>      ! Gerund #6: -{bY}{A}r{A}k
%<ger8%>      ! (Future) gerund #8: -{bY}{A}c{A}{k}
%<impp%>      ! Imperfect participle #1: -{bY}{A}r{A}{k}

%<cont%>      ! Present continuous: -{bI}yor
%<opt%>       ! Optative: -{bY}{A}
%<past%>       ! Past: -{D}{I}
%<evid%>      ! Evidential past: -m{I}ş
%<imp%>       ! Imperative: -ø
%<fut%>       ! Future: -{bY}{A}c{A}{k}
%<cond%>      ! Conditional: -s{A}
%<pii%>       ! Imperfect: -m{A}kt{A}
%<oblig%>     ! Obligative: -m{A}l{I}
%<aor%>       ! Aorist: -{A}r or -{bI}r
%<pih%>       ! Habitual: Aorist + -{D}{I}

%<qst%>       ! Question particle

%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side) 
%<lquot%>     ! Quote marker (left hand side) 
%<rpar%>      ! Parenthetical marker (right hand side) 
%<lpar%>      ! Parenthetical marker (left hand side) 


%{e%}         ! e or ''
%{a%}         ! a or ''
%{A%}         ! Archivowel {A}: e (a)
%{I%}         ! Archivowel {I}: u ü i (ı)
%{bN%}        ! Buffer 'n': -n- or -ø-
%{bY%}        ! Buffer 'y': -y- or -ø-
%{bI%}        ! Buffer 'i': -i- or -ø-
%{bS%}        ! Buffer 's': -s- or -ø-
%{D%}         ! Archicons {D}: d t
%{K%}         ! Archicons {K}: k ğ y
%{p%}         ! Archicons {p}: p b 
%{k%}         ! Archicons {k}: k ğ g
%{t%}         ! Archicons {t}: t d
%{c%}         ! Archicons {c}: c ç 
%{LN%}        ! Archicons {LN}: l n
%{compn%}     ! Do not allow possessives
%{dup%}       ! 
%{del%}       ! 
%{dels%}      ! 
%{pA%}        ! Unharmonised 'a'
%{pU%}        ! Unharmonised 'u'
%{pO%}        ! Unharmonised 'o'

%{nin%}
%{nın%}
%{nun%}
%{un%}
%{in%}

%>            ! Morpheme boundary
% 

LEXICON Root
Nouns ; 
Numerals ; 
Adjectives ; 
Adverbs ; 
Postpositions ; 
Conjunctions ; 
Pronouns ;
Determiners ;
ProperNouns ;
Acronyms ;
Punctuation ;
Interjections ;
Verbs ;
Particles ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!! Case morphology

LEXICON clit

!%+mi%<qst%>:% m%{I%}%{bS%} poss-end ;
%+mi%<qst%>:% m%{I%} V-PERS-COP-G2-NOSG3-FINAL ;
%+mi%<qst%>:% m%{I%} # ;
# ;

LEXICON clit-cop

%+%i%<cop%>:% i V-PERS-COP-G2-NOSG3 ;  ! Dir/LR
%+%i%<cop%>%<past%>:% i%>%{D%}%{I%} V-PERS-G1 ;  ! Dir/LR
%+%i%<cop%>%<cond%>:% i%>s%{A%} V-PERS-G1 ;  ! Dir/LR
%+%i%<cop%>%<evid%>:% i%>%{bY%}m%{I%}ş V-PERS-COP-G2 ;  ! Dir/LR
%+%i%<cop%>%<rel%>:% i%>ken #  ; ! Dir/LR
%+%i%<cop%>: V-PERS-COP-G2-NOSG3 ;
%+%i%<cop%>%<past%>:%>%{D%}%{I%} V-PERS-G1 ;  
%+%i%<cop%>%<cond%>:%>%{bY%}s%{A%} V-PERS-G1 ;
%+%i%<cop%>%<evid%>:%>%{bY%}m%{I%}ş V-PERS-COP-G2 ;
%+%i%<cop%>%<rel%>:%>ken #  ;
clit ; 
!# ;

LEXICON DAKI

%<attr%>: # ;
%<subst%>: clit-cop ; 

LEXICON case1                     
%<loc%>:%>%{D%}%{A%} clit-cop ;
%<gen%>:%>%{bN%}%{I%}n clit-cop ;
%<loc%>:%>%{D%}%{A%}%{K%}i DAKI ; 

LEXICON case2                    
%<acc%>:%>%{bY%}%{I%} clit-cop ;
%<abl%>:%>%{D%}%{A%}n clit-cop ;
%<dat%>:%>%{bY%}%{A%} clit-cop ;
%<ins%>:%>%{bY%}l%{A%} clit-cop ;
!%<ins%>:%>%{bY%}l%{A%}n # ;

LEXICON case1p3
%<loc%>:%>%{bN%}d%{A%} clit-cop ;
%<gen%>:%>%{bN%}%{I%}n clit-cop ;
%<loc%>:%>%{bN%}%{D%}%{A%}%{K%}i DAKI ;

LEXICON case2p3
%<acc%>:%>%{bN%}%{I%} clit-cop ;
%<abl%>:%>%{bN%}d%{A%}n clit-cop ;
%<dat%>:%>%{bN%}%{A%} clit-cop ;
%<ins%>:%>%{bY%}l%{A%} clit-cop ;
!%<ins%>:%>%{bY%}l%{A%}n # ;

!! Possessive morphology

LEXICON poss-end

%<px1sg%>:%>%{bI%}m # ;
%<px2sg%>:%>%{bI%}n # ;
%<px1pl%>:%>%{bI%}m%{I%}z # ;
%<px2pl%>:%>%{bI%}n%{I%}z # ;
%<px3sg%>:%>%{bS%}%{I%} # ;
%<px3pl%>:%>l%{A%}r%{I%} # ;

LEXICON poss12-2                    
%<px1sg%>:%>%{bI%}m # ;
%<px2sg%>:%>%{bI%}n # ;
%<px1pl%>:%>%{bI%}m%{I%}z # ;
%<px2pl%>:%>%{bI%}n%{I%}z # ;

%<px1sg%>:%>%{bI%}m case2 ;
%<px2sg%>:%>%{bI%}n case2 ;
%<px1pl%>:%>%{bI%}m%{I%}z case2 ;
%<px2pl%>:%>%{bI%}n%{I%}z case2 ;


LEXICON poss12-1
%<px1sg%>:%>%{bI%}m # ;
%<px2sg%>:%>%{bI%}n # ;
%<px1pl%>:%>%{bI%}m%{I%}z # ;
%<px2pl%>:%>%{bI%}n%{I%}z # ;


%<px1sg%>:%>%{bI%}m case1 ;
%<px2sg%>:%>%{bI%}n case1 ;
%<px1pl%>:%>%{bI%}m%{I%}z case1 ;
%<px2pl%>:%>%{bI%}n%{I%}z case1 ;

LEXICON poss3-2

%<px3sg%>:%>%{bS%}%{I%} # ;
%<px3pl%>:%>l%{A%}r%{I%} # ;


%<px3sg%>:%>%{bS%}%{I%} case2p3 ;
%<px3pl%>:%>l%{A%}r%{I%} case2p3 ;

LEXICON poss3-1

%<px3sg%>:%>%{bS%}%{I%} # ;
%<px3pl%>:%>l%{A%}r%{I%} # ;


%<px3sg%>:%>%{bS%}%{I%} case1p3 ;
%<px3pl%>:%>l%{A%}r%{I%} case1p3 ;

LEXICON poss

poss12-1  ;
poss12-2  ;
poss3-1 ;
poss3-2 ;

!! Combinatorial morphology

LEXICON POSS12

poss12-1 ;
poss12-2 ;
%<pl%>:%>l%{A%}r poss12-1 ;
%<pl%>:%>l%{A%}r poss12-2 ;

LEXICON POSS3

poss3-1 ;
poss3-2 ;
%<pl%>:%>l%{A%}r poss3-1 ;
%<pl%>:%>l%{A%}r poss3-2 ;

LEXICON POSS

POSS12 ;
POSS3 ; 

LEXICON CASE1

case1 ;
%<pl%>:%>l%{A%}r case1 ;
poss12-1 ; 
poss3-1;

LEXICON CASE2

case2 ;
%<pl%>:%>l%{A%}r case2 ;
poss12-2 ;
poss3-2; 

LEXICON CASE 

CASE1 ; 
CASE2 ; 

LEXICON NINFL-COMMON

POSS ;
CASE1 ; 
CASE2 ; 

LEXICON CASE-DEM 

case1 ; 
case2 ; 



LEXICON SUBST

# ; 
clit-cop ; 
%<advl%>: # ; 
%<attr%>: clit-cop ;  ! This is the attributive adjective use of -kI, TRmorph <ki>
!%<n%>%<attr%>:%>k%{I%} # ;  ! This is the attributive adjective use of -kI, TRmorph <ki>
!%<n%>%<attr%>:%>k%{I%} NINFL-COMMON ; ! This is the attribute adjective 0 derived into a noun, TRmorph <ki>
%<loc%>:%>%{D%}%{A%}%{k%}i DAKI ; ! This makes an attributive phrase from a noun, TRmorph <loc><ki>
%<pl%>:%>l%{A%}r clit ; 
NINFL-COMMON  ; 



LEXICON N1

%<n%>: # ; 
%<n%>: SUBST ; 

LEXICON NP-TOP

%<np%>%<top%>: # ; 
%<adj%>:%>l%{I%}%{k%} # ; 
%<np%>%<top%>:%' NINFL-COMMON  ; 
%<np%>%<top%>%<loc%>:%'%>%{D%}%{A%}%{k%}i DAKI ; ! This makes an attributive phrase from a noun, TRmorph <loc><ki>

LEXICON NP-ACR

%<np%>%<acr%>: # ; 
%<np%>%<acr%>:%' NINFL-COMMON  ; 


LEXICON NP-AL

%<np%>%<al%>: # ; 
%<np%>%<al%>:%' NINFL-COMMON  ; 

LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: # ; 
%<np%>%<ant%>%<m%>:%' NINFL-COMMON  ; 

LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: # ; 
%<np%>%<ant%>%<f%>:%' NINFL-COMMON  ; 

LEXICON NP-COG-MF

%<np%>%<cog%>%<mf%>: # ; 
%<np%>%<cog%>%<mf%>:%' NINFL-COMMON  ; 

LEXICON A2

%<adj%>: # ; 

LEXICON A1

%<adj%>: # ; 
%<adj%>%<subst%>: SUBST ; 

LEXICON A3

%<adj%>: # ; 
%<n%>: # ; 
%<n%>:%' NINFL-COMMON  ;  ! Dir/LR
%<n%>: NINFL-COMMON  ; 
%<n%>%<pl%>:%>l%{A%}r # ; 
%<n%>%<pl%>:%>l%{A%}r NINFL-COMMON  ; 
%<n%>%<pl%>:%>l%{A%}r%' NINFL-COMMON  ;  ! Dir/LR

LEXICON ADV

%<adv%>: # ; 
%<adv%>%<attr%>: # ;  ! <adv> as <adj> function

LEXICON DIGITLEX

%<num%>: # ;


LEXICON NUM

%<num%>: # ;
%<num%>%<subst%>: NINFL-COMMON ;
%<num%>%<ord%>:%>%{bI%}nc%{I%} # ; ! This is <D_IncI> in TRmorph
%<num%>%<ord%>:%>%{bI%}nc%{I%} NINFL-COMMON ; ! This is <D_IncI> in TRmorph

LEXICON V-PERS-G1

%<p1%>%<sg%>:%>m # ;
%<p2%>%<sg%>:%>n # ;
%<p2%>%<frm%>%<sg%>:%>n%{I%}z # ;
%<p3%>%<sg%>:%> # ;
%<p1%>%<pl%>:%>%{k%} # ;
%<p2%>%<pl%>:%>n%{I%}z # ;
%<p3%>%<pl%>:%>l%{A%}r # ;


LEXICON V-PERS-COP-G2-NOSG3-FINAL

%<p1%>%<sg%>:%>%{bY%}%{I%}m # ;
%<p2%>%<sg%>:%>s%{I%}n  # ;
%<p2%>%<frm%>%<sg%>:%>s%{I%}n%{I%}z  # ;
%<p1%>%<pl%>:%>%{bY%}%{I%}z  # ;
%<p2%>%<pl%>:%>s%{I%}n%{I%}z  # ;
%<p3%>%<pl%>:%>l%{A%}r  # ;


LEXICON V-PERS-COP-G2-NOSG3

%<p1%>%<sg%>:%>%{bY%}%{I%}m clit ;
%<p2%>%<sg%>:%>s%{I%}n  clit ;
%<p2%>%<frm%>%<sg%>:%>s%{I%}n%{I%}z  clit ;
%<p1%>%<pl%>:%>%{bY%}%{I%}z  clit ;
%<p2%>%<pl%>:%>s%{I%}n%{I%}z  clit ;
%<p3%>%<pl%>:%>l%{A%}r  clit ;


LEXICON V-PERS-COP-G2

%<p3%>%<sg%>:%>  clit ;
V-PERS-COP-G2-NOSG3 ; 

LEXICON V-PERS-OPT-G3

%<p1%>%<sg%>:%>y%{I%}m # ;
%<p2%>%<sg%>:%>s%{I%}n # ;
%<p2%>%<frm%>%<sg%>:%>s%{I%}n%{I%}z # ;
%<p3%>%<sg%>:%> # ;
%<p1%>%<pl%>:%>l%{I%}m # ;
%<p2%>%<pl%>:%>s%{I%}n%{I%}z # ;
%<p3%>%<pl%>:%>l%{A%}r # ;

LEXICON V-PERS-IMP-G4

%<p2%>%<sg%>:%>s%{A%}n%{A%} # ; ! Dir/LR
%<p2%>%<sg%>:%> # ;
%<p2%>%<frm%>%<sg%>:%>%{bY%}%{I%}n #;
!%<p2%>%<frm%>%<sg%>:%>%{bY%}%{I%}n%{I%}z #;
!%<p2%>%<frm%>%<sg%>:%>s%{A%}n%{I%}z%{A%} # ;
%<p3%>%<sg%>:%>s%{I%}n # ;
%<p2%>%<pl%>:%>%{bY%}%{I%}n #;
%<p2%>%<pl%>:%>%{bY%}%{I%}n%{I%}z #;
%<p2%>%<pl%>:%>s%{A%}n%{I%}z%{A%} # ;
%<p3%>%<pl%>:%>s%{I%}nl%{A%}r # ;

LEXICON VINFL-COMMON

%<ger1%>:%>m%{A%} # ;
%<ger1%>:%>m%{A%} NINFL-COMMON ;
%<ger2%>:%>m%{A%}%{K%} # ;
%<ger2%>:%>m%{A%}%{K%} NINFL-COMMON ;
%<ger3%>:%>%{D%}%{I%}%{k%} # ;
%<ger3%>:%>%{D%}%{I%}%{k%} NINFL-COMMON ;
%<ger4%>:%>%{bY%}%{I%}ş # ;
%<ger4%>:%>%{bY%}%{I%}ş NINFL-COMMON ;
%<ger5%>:%>%{bY%}%{A%}n # ;
%<ger5%>:%>%{bY%}%{A%}n NINFL-COMMON ;
%<ger8%>:%>%{bY%}%{A%}c%{A%}%{k%} # ;
%<ger8%>:%>%{bY%}%{A%}c%{A%}%{k%} NINFL-COMMON ;
%<impp%>:%>%{bY%}%{A%}r%{A%}%{k%} # ;
%<impp%>:%>%{bY%}%{A%}r%{A%}%{k%} NINFL-COMMON ;

%<prc1%>:%>%{bY%}%{I%}p # ;
%<prc1%>:%>%{bY%}%{I%}p clit-cop ;

%<gna1%>:%>%{bY%}%{I%}p # ;
%<gna1%>:%>%{bY%}%{I%}p clit-cop ;

%<gna9%>:%>%{bY%}%{A%} # ;
%<gna9%>:%>%{bY%}%{A%} clit-cop ;

%<cont%>:%>%{bI%}yor V-PERS-COP-G2 ;
!%<cont%>:%>%{bI%}yor clit-cop ; ! This should be renamed e.g. <cont><past> 
%<opt%>:%>%{bY%}%{A%} V-PERS-OPT-G3 ;
%<past%>:%>%{D%}%{I%} V-PERS-G1 ;
%<evid%>:%>m%{I%}ş V-PERS-COP-G2 ;
%<imp%>: V-PERS-IMP-G4 ;
%<fut%>:%>%{bY%}%{A%}c%{A%}%{k%} V-PERS-COP-G2 ; 
%<cond%>:%>s%{A%} V-PERS-G1 ;
%<pii%>:%>m%{A%}kt%{A%} V-PERS-COP-G2 ;
%<oblig%>:%>m%{A%}l%{I%} V-PERS-COP-G2 ;

!! Enumerate the possibilities for verb combinations in Turkish (go go turmorph!)
!! Unmarked entries get -t for causative if they end in l, r, or a vowel, otherwise -DIr. 
!
!LEXICON VIVINFL-CAUS_T-AOR_AR
!%<v%>%<iv%>: VINFL-COMMON ;
!%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
!%<v%>%<iv%>%<caus%>:%>t  VINFL-COMMON ;
!
LEXICON VIVINFL-CAUS_DIR-AOR_AR
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;
!
!LEXICON VIVINFL-CAUS_AR-AOR_IR
!%<v%>%<iv%>: VINFL-COMMON ;
!%<v%>%<iv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
!%<v%>%<iv%>%<caus%>:%>%{A%}r VINFL-COMMON ;
!
LEXICON VIVINFL-CAUS_AR-AOR_AR
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>%{A%}r VINFL-COMMON ;


!LEXICON VIVINFL-CAUS_ART-AOR_AR
!%<v%>%<iv%>: VINFL-COMMON ;
!%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
!%<v%>%<iv%>%<caus%>:%>%{A%}rt VINFL-COMMON ;
!

LEXICON VIVINFL-CAUS_DIR-AOR_IR

%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<aor%>:%>%{bY%}%{A%}bil%>%{bI%}r V-PERS-COP-G2 ; ! add to all!
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;

!
LEXICON VIVINFL-CAUS_DIR-AOR_AR
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<aor%>:%>%{bY%}%{A%}bil%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;
!
LEXICON VIVINFL-CAUS_IR-AOR_AR
! art<iv><caus_ir><aor_ar>
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>%{I%}r VINFL-COMMON ;
!
LEXICON VIVINFL-CAUS_IRREG-AOR_IR
! IRREG does not get causative (it's in the lexicon ?)
! gel<iv><caus_irreg>
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
!
LEXICON VIVINFL-CAUS_IRREG-AOR_AR
! IRREG does not get causative (it's in the lexicon ?)
! ay<iv><caus_irreg><aor_ar>
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
!
!LEXICON VIVINFL-CAUS_IT-AOR_IR
!%<v%>%<iv%>: VINFL-COMMON ;
!%<v%>%<iv%>%<caus%>:%>%{I%}t VINFL-COMMON ;
!%<v%>%<iv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
!
LEXICON VIVINFL-CAUS_IT-AOR_AR
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<caus%>:%>%{I%}t VINFL-COMMON ;

LEXICON VIVINFL-CAUS_T-AOR_IR
%<v%>%<iv%>: VINFL-COMMON ;
%<v%>%<iv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<iv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<iv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<iv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<iv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<iv%>%<caus%>:%>t VINFL-COMMON ;
!
!!! TV BELOW
!
!LEXICON VTVINFL-CAUS_T-AOR_AR
!
!%<v%>%<tv%>: VINFL-COMMON ;
!%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
!%<v%>%<tv%>%<caus%>:%>t VINFL-COMMON ;
!%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_DIR-AOR_AR
! aç<t><tv><aor_ar>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_AR-AOR_IR
! kulluk e<t><tv><caus_ar>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{A%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_AR-AOR_AR
! gi<t><tv><caus_ar><aor_ar> 
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{A%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_ART-AOR_AR
! çık<tv><caus_art><aor_ar>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{A%}rt VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_DIR-AOR_IR
! al<tv><caus_dir>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>%<aor%>:%>%{bY%}%{A%}%>m%{A%}z V-PERS-G1 ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_DIR-AOR_AR
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{D%}%{I%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_IR-AOR_AR  
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{I%}r VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_IRREG-AOR_AR
! IRREG does not get causative
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_IT-AOR_IR
! üş<tv><caus_it>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{I%}t VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_IT-AOR_AR
! ak<tv><caus_it><aor_ar>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{A%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{A%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>%{I%}t VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;
!
LEXICON VTVINFL-CAUS_T-AOR_IR
! bekle<tv><caus_t>
%<v%>%<tv%>: VINFL-COMMON ;
%<v%>%<tv%>%<abil%>:%>%{bY%}%{A%}bil VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>:%>%{bY%}%{A%}%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<abil%>%<neg%>%<aor%>:%>%{bY%}%{A%}%>m%{A%} V-PERS-G1 ;
%<v%>%<tv%>%<neg%>:%>m%{A%} VINFL-COMMON ;
%<v%>%<tv%>%<neg%>%<aor%>:%>m%{A%}z V-PERS-COP-G2 ;
%<v%>%<tv%>%<aor%>:%>%{bI%}r V-PERS-COP-G2 ;
%<v%>%<tv%>%<pih%>:%>%{bI%}r%>%{D%}%{I%} V-PERS-G1 ;
%<v%>%<tv%>%<caus%>:%>t VINFL-COMMON ;
%<v%>%<tv%>%<pass%>:%>%{bI%}%{LN%} VINFL-COMMON ;



LEXICON INTERJ

%<ij%>: # ;


LEXICON POST

%<post%>: # ;

LEXICON CC

%<cnjcoo%>: # ; 

LEXICON CS

%<cnjsub%>: # ; 

LEXICON CA

%<cnjadv%>: # ; 

LEXICON PRONPERS

ben%<prn%>%<pers%>%<p1%>%<sg%>:ben # ;
ben%<prn%>%<pers%>%<p1%>%<sg%>%<dat%>:bana # ;
ben%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:benim # ;
ben%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:beni # ;
ben%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:bende # ;
ben%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:benden # ;
!ben%<prn%>%<pers%>%<p1%>%<sg%>%<ins%>:benimle # ;
!ben%<prn%>%<pers%>%<p1%>%<sg%>%<ins%>:benle # ;

sen%<prn%>%<pers%>%<p2%>%<sg%>:sen # ;
sen%<prn%>%<pers%>%<p2%>%<sg%>%<dat%>:sana # ;
sen%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:senin # ;
sen%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:seni # ;
sen%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:sende # ;
sen%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:senden # ;
!sen%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:senimle # ;
!sen%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:senle # ;

o%<prn%>%<pers%>%<p3%>%<sg%>:o # ;
o%<prn%>%<pers%>%<p3%>%<sg%>%<dat%>:ona # ;
o%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:onun # ;
o%<prn%>%<pers%>%<p3%>%<sg%>%<acc%>:onu # ;
o%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:ondan # ;
o%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:onda # ;
!o%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:oimle # ;
!o%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:ole # ;

biz%<prn%>%<pers%>%<p1%>%<pl%>:biz # ;
biz%<prn%>%<pers%>%<p1%>%<pl%>%<dat%>:bize # ;
biz%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:bizim # ;
biz%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:bizi # ;
biz%<prn%>%<pers%>%<p1%>%<pl%>%<abl%>:bizde # ;
biz%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:bizden # ;
!biz%<prn%>%<pers%>%<p1%>%<pl%>%<ins%>:bizimle # ;
!biz%<prn%>%<pers%>%<p1%>%<pl%>%<ins%>:bizle # ;

siz%<prn%>%<pers%>%<p2%>%<pl%>:siz # ;
siz%<prn%>%<pers%>%<p2%>%<pl%>%<dat%>:size # ;
siz%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:sizin # ;
siz%<prn%>%<pers%>%<p2%>%<pl%>%<acc%>:sizi # ;
siz%<prn%>%<pers%>%<p2%>%<pl%>%<abl%>:sizde # ;
siz%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:sizden # ;
!siz%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:sizimle # ;
!siz%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:sizle # ;

onlar%<prn%>%<pers%>%<p3%>%<pl%>:onlar # ;
onlar%<prn%>%<pers%>%<p3%>%<pl%>%<dat%>:onlara # ;
onlar%<prn%>%<pers%>%<p3%>%<pl%>%<gen%>:onların # ;
onlar%<prn%>%<pers%>%<p3%>%<pl%>%<acc%>:onları # ;
onlar%<prn%>%<pers%>%<p3%>%<pl%>%<abl%>:onlardan # ;
onlar%<prn%>%<pers%>%<p3%>%<pl%>%<loc%>:onlarda # ;
!onlar%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:onlarimle # ;
!onlar%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:onlarle # ;

LEXICON PRON-DEM

%<prn%>%<dem%>: CASE-DEM ; 
%<prn%>%<dem%>%<pl%>:%>l%{A%}r # ; 
%<prn%>%<dem%>%<pl%>:%>l%{A%}r CASE-DEM ; 

LEXICON PRON-ITG

%<prn%>%<itg%>: # ; 
%<prn%>%<itg%>: NINFL-COMMON ; 

!! FIXME: This needs fixing 
LEXICON PRON-ITG-NERE

%<prn%>%<itg%>:e # ; 
%<prn%>%<itg%>:e NINFL-COMMON ; 
%<prn%>%<itg%>%<loc%>:de clit ;  ! Dir/LR
%<prn%>%<itg%>%<abl%>:den clit ;  ! Dir/LR


LEXICON PRON-IND

%<prn%>%<ind%>: # ; 
%<prn%>%<ind%>: NINFL-COMMON ; 

LEXICON PRON-REF

%<prn%>%<ref%>: # ; 
%<prn%>%<ref%>: NINFL-COMMON ; 

LEXICON PRON-ADV

%<prn%>%<adv%>: # ; 
%<prn%>%<adv%>: NINFL-COMMON ; 


LEXICON DET-DEM

%<det%>%<dem%>: # ;

LEXICON DET-IND

%<det%>%<ind%>: # ;

LEXICON DET-QNT

%<det%>%<qnt%>: # ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Conjunctions

ve:ve CC ; ! "and"
ama:ama CC ; ! "but"
ya:ya CC ; ! "either/or"
da:da CC ; ! ""
da:de CC ; ! "" ! Dir/LR
fakat:fakat CC ; ! ""
ya% da:ya% da CC ; ! ""

LEXICON Postpositions

ile:ile POST ; ! "with"
rağmen:rağmen POST ; ! ""

LEXICON Pronouns

PRONPERS ;

bu%<prn%>%<dem%>:bu # ; ! "this"
bu:bun PRON-DEM ; ! "this"
şu%<prn%>%<dem%>:şu # ; ! "that"
şu:şun PRON-DEM ; ! "that"

ne:ne PRON-ITG ; ! "what" ***CHECK*** adverb ?
ne% için:ne% için PRON-ITG ; ! "what" ***CHECK*** adverb ?
hangi:hangi PRON-ITG ; ! ""
nere:ner PRON-ITG-NERE ; ! ""
kim:kim PRON-ITG ; ! "who"

birisi:birisi PRON-IND ; ! "anyone, anybody"
öteki:öteki PRON-IND ; ! "other"

hiçbir% şey:hiçbir% şey PRON-IND ; ! "nothing" ! Use/MT

kendi:kendi PRON-REF ; ! "self"
öz:öz PRON-REF ; ! "self"

ora:ora PRON-ADV ; ! "" 

LEXICON Determiners

bir:bir DET-IND ;  ! "a"
başka:başka DET-IND ;  ! ""
diğer:diğer DET-IND ;  ! "other"
öteki:öteki DET-IND ;  ! "other"
öbür:öbür DET-IND ;  ! "other"
aynı:aynı DET-IND ;  ! "same"
hiçbir:hiçbir DET-IND ;  ! "no"
herhangi:herhangi DET-IND ;  ! "any"

bu:bu DET-DEM ;  ! "this"
şu:şu DET-DEM ;  ! "that"

bütün:bütün DET-QNT;  ! "all"
her:her DET-QNT ;  ! "every"
tüm:tüm DET-QNT;  ! "all"
birkaç:birkaç DET-QNT;  ! "all"
birtakım:birtakım DET-QNT;  ! "some"
bazı:bazı DET-QNT;  ! "some"
kimi:kimi DET-QNT;  ! "some"
fazla:fazla DET-QNT;  ! "much, many"
çok:ço%{k%} DET-QNT; ! "much, many"
biraz:biraz DET-QNT;  ! "a little"
birçok:birçok DET-QNT ;  ! "many"
birçok:bir% çok DET-QNT ;  ! "many" Dir/LR

LEXICON Numerals 

sıfır:sıfır NUM ; ! "zero"
bir:bir NUM ; ! "one"
iki:iki NUM ; ! "two"
iki:İki NUM ; ! "two" ! Dir/LR
üç:üç NUM ; ! "three"
dört:dör%{t%} NUM ; ! "four"
beş:beş NUM ; ! "five"
altı:altı NUM ; ! "six"
yedi:yedi NUM ; ! "seven"
sekiz:sekiz NUM ; ! "eight"
dokuz:dokuz NUM ; ! "nine"
on:on NUM ; ! "ten"
yirmi:yirmi NUM ; ! "twenty"
otuz:otuz NUM ; ! "thirty"
kırk:kırk NUM ; ! "fourty"
elli:elli NUM ; ! "fifty"
altmış:altmış NUM ; ! "sixty"
yetmiş:yetmiş NUM ; ! "seventy"
seksen:seksen NUM ; ! "eighty"
doksan:doksan NUM ; ! "ninety"
yüz:yüz NUM ; ! "hundred"
bin:bin NUM ; ! "thousand"
milyon:milyon NUM ; ! "million"
milyar:milyar NUM ; ! "billion"
trilyon:trilyon NUM ; ! "trillion"

LEXICON Nouns

ev:ev N1 ; ! "house"
masa:masa N1 ; ! "table"
ağaç:ağa%{c%} N1 ; ! "tree"
el:el N1 ; ! "hand "
etraf:etraf N1 ; ! "sides"
su:su N1 ; ! "water"
kova:kova N1 ; ! "bucket"
ana:ana N1 ; ! ""
an:an N1 ; ! ""
anne:anne N1 ; ! ""
arka:arka N1 ; ! ""
bahçe:bahçe N1 ; ! ""
bira:bira N1 ; ! ""
bugün:bugün N1 ; ! ""
cevap:ceva%{p%} N1 ; ! ""
dışarı:dışarı N1 ; ! ""
çocuk:çocu%{k%} N1 ; ! ""
yarın:yarın N1 ; ! ""
yaş:yaş N1 ; ! "year"
yer:yer N1 ; ! ""
zaman:zaman N1 ; ! ""
dün:dün N1 ; ! ""
ev:ev N1 ; ! ""
göz:göz N1 ; ! ""
hava:hava N1 ; ! ""
hayvan:hayvan N1 ; ! ""
insan:insan N1 ; !""
insan:İnsan N1 ; ! "" ! Dir/LR
kafa:kafa N1 ; ! ""
kız% kardeş:kız% kardeş N1 ; ! ""
kedi:kedi N1 ; ! ""
kız:kız N1 ; ! ""
köpek:köpe%{k%} N1 ; ! ""
kuş:kuş N1 ; ! ""
sandalye:sandalye N1 ; ! ""
ses:ses N1 ; ! ""
şey:şey N1 ; ! ""
soğuk:soğu%{k%} N1 ; ! ""
soğuk:soğu%{k%} N1 ; ! ""
soru:soru N1 ; ! ""
masa:masa N1 ; ! ""
oğlan:oğlan N1 ; ! ""
ön:ön N1 ; ! ""
oyun:oyun N1 ; ! ""
pencere:pencere N1 ; ! ""
iç:iç N1 ; ! ""

LEXICON ProperNouns

Ahmet:Ahmet NP-ANT-M ; ! ""
Ayşe:Ayşe NP-ANT-F ; ! ""

LEXICON Acronyms

AB:AB%{nin%} NP-ACR ; ! AB'nin
ABD:ABD%{nin%} NP-ACR ; ! ABD'nin
AFP:AFP%{nin%} NP-ACR ; ! AFP'nin
AKP:AKP%{nin%} NP-ACR ; ! "AKP'nin"
AP:AP%{nin%} NP-ACR ; ! "AP'nin"
BBC:BBC%{nin%} NP-ACR ; ! "BBC'nin"
BH:BH%{nin%} NP-ACR ; ! BH'nin
BHF:BHF%{nin%} NP-ACR ; ! "BHF'nin"
BM:BM%{nin%} NP-ACR ; ! BM'nin
BNR:BNR%{nin%} NP-ACR ; ! "BNR'nin"
BSP:BSP%{nin%} NP-ACR ; ! "BSP'nin"
BTA:BTA%{nın%} NP-ACR ; ! "BTA'nın"
CNA:CNA%{nın%} NP-ACR ; ! "CNA'nın"
CNN:CNN%{in%} NP-ACR ; ! "CNN'in"
DP:DP%{nin%} NP-ACR ; ! "DP'nin"
DPA:DPA%{nın%} NP-ACR ; ! "DPA'nın"
DPMNE:DPMNE%{nin%} NP-ACR ; ! "DPMNE'nin"
DS:DS%{in%} NP-ACR ; ! "DS'in"
DSS:DSS%{nin%} NP-ACR ; ! "DSS'nin"
DUI:DUI%{nin%} NP-ACR ; ! "DUI'nin"
EBRD:EBRD%{nin%} NP-ACR ; ! "EBRD'nin"
EUFOR:EUFOR%{un%} NP-ACR ; ! EUFOR'nun
EULEX:EULEX%{in%} NP-ACR ; ! "EULEX'in"
FENA:FENA%{nın%} NP-ACR ; ! "FENA'nın"
FT:FT%{nin%} NP-ACR ; ! "FT'nin"
GDA:GDA%{nın%} NP-ACR ; ! "GDA'nın"
GSYİH:GSYİH%{nin%} NP-ACR ; ! "GSYİH'nin"
HDZ:HDZ%{nin%} NP-ACR ; ! "HDZ'nin"
HINA:HINA%{nın%} NP-ACR ; ! "HINA'nın"
HRT:HRT%{nin%} NP-ACR ; ! "HRT'nin"
ICTY:ICTY%{nin%} NP-ACR ; ! "ICTY'nin"
IMF:IMF%{nin%} NP-ACR ; ! "IMF'nin"
KFOR:KFOR%{un%} NP-ACR ; ! "KFOR'un"
KKTC:KKTC%{nin%} NP-ACR ; ! "KKTC'nin"
MPA:MPA%{nın%} NP-ACR ; ! "MPA'nın"
NATO:NATO%{nun%} NP-ACR ; ! NATO'nun
NTV:NTV%{nin%} NP-ACR ; ! "NTV'nin"
PACE:PACE%{nin%} NP-ACR ; ! "PACE'nin"
PASOK:PASOK%{un%} NP-ACR ; ! "PASOK'un"
PBS:PBS%{nin%} NP-ACR ; ! "PBS'nin"
PIC:PIC%{nin%} NP-ACR ; ! "PIC'nin"
PKK:PKK%{nın%} NP-ACR ; ! PKK'nın
PNL:PNL%{nin%} NP-ACR ; ! "PNL'nin"
PSD:PSD%{nin%} NP-ACR ; ! "PSD'nin"
RTS:RTS%{nin%} NP-ACR ; ! "RTS'nin"
SAA:SAA%{nın%} NP-ACR ; ! "SAA'nın"
SC:SC%{nin%} NP-ACR ; ! "SC'nin"
SDP:SDP%{nin%} NP-ACR ; ! "SDP'nin"
SDS:SDS%{nin%} NP-ACR ; ! "SDS'nin"
SDSM:SDSM%{nin%} NP-ACR ; ! "SDSM'nin"
SFOR:SFOR%{un%} NP-ACR ; ! SFOR'un
SP:SP%{nin%} NP-ACR ; ! "SP'nin"
SRNA:SRNA%{nın%} NP-ACR ; ! "SRNA'nın"
SRS:SRS%{nin%} NP-ACR ; ! "SRS'nin"
STK:STK%{nın%} NP-ACR ; ! "STK'nın"
TRT:TRT%{nin%} NP-ACR ; ! "TRT'nin"
TV:TV%{nin%} NP-ACR ; ! "TV'nin"
UAD:UAD%{nin%} NP-ACR ; ! "UAD'nin"
UNDP:UNDP%{nin%} NP-ACR ; ! "UNDP'nin"
UNMIK:UNMIK%{in%} NP-ACR ; ! "UNMIK'in"
VMRO:VMRO%{nun%} NP-ACR ; ! "VMRO'nun"
VOA:VOA%{nın%} NP-ACR ; ! "VOA'nın"
YTD:YTD%{nin%} NP-ACR ; ! "YTD'nin"
bTV:bTV%{nin%} NP-ACR ; ! "bTV'nin"

LEXICON Adjectives 

var:var A2 ; ! ""
yok:yok A2 ; ! ""
büyük:büyük A1 ; ! "big"
yaşlı:yaşlı A1 ; ! "old"
küçük:küçü%{k%} A1 ; ! ""
büyük:büyü%{k%} A1 ; ! ""
eski:eski A1 ; ! ""
güzel:güzel A1 ; ! ""
mutlu:mutlu A1 ; ! ""
ulu:ulu A1 ; ! ""
yakın:yakın A1 ; ! ""
seri:seri A1 ; ! "fast"
sıcak:sıcak A1 ; ! "hot"
sıcak:sıcak A1 ; ! "hot"

LEXICON Adverbs 

iyi:iyi ADV ; ! "good!"
bu% yüzden:bu% yüzden ADV ; ! ""
hâlâ:hâlâ ADV ; ! ""
fakat:fakat ADV ; ! ""
yeterince:yeterince ADV ; ! "enough, very"
yavaşça:yavaşça ADV ; ! "slowly"
çabukça:çabukça ADV ; ! "promptly"
dikkatlice:dikkatlice ADV ; ! "carefully"
beraber:beraber ADV ; ! ""
birlikte:birlikte ADV ; ! ""
güzel:güzel ADV ; ! ""
her% zaman:her% zaman ADV ; ! ""
sonra:sonra ADV ; ! ""
yine:yine ADV ; ! ""
çok:çok ADV ; ! ""

LEXICON Verbs

ol:ol VIVINFL-CAUS_DIR-AOR_IR ; ! "be"
gel:gel VIVINFL-CAUS_IRREG-AOR_IR ; ! "come"
git:gi%{t%} VIVINFL-CAUS_AR-AOR_AR ; ! "go"
iç:iç VTVINFL-CAUS_IR-AOR_AR ; ! "drink"
söyle:söyle VTVINFL-CAUS_T-AOR_IR ; ! "say"
yap:yap VTVINFL-CAUS_DIR-AOR_AR ; ! "do, make"
ye:y%{e%} VTVINFL-CAUS_DIR-AOR_IR ; ! ""
gezin:gezin VIVINFL-CAUS_DIR-AOR_IR ; ! ""
düşün:düşün VTVINFL-CAUS_DIR-AOR_IR ; ! ""
konuş:konuş VIVINFL-CAUS_DIR-AOR_IR  ; ! "talk"
koş:koş VIVINFL-CAUS_DIR-AOR_AR  ; ! "run"
otur:otur VIVINFL-CAUS_T-AOR_IR  ; ! "sit"
oy:oy VTVINFL-CAUS_DIR-AOR_AR  ; ! "carve"
oy:oy al VIVINFL-CAUS_DIR-AOR_IR  ; ! "receive vote"
oyna:oyna VTVINFL-CAUS_T-AOR_IR  ; ! "play"
sakla:sakla VTVINFL-CAUS_T-AOR_IR  ; ! "hide"
say:say VTVINFL-CAUS_DIR-AOR_AR  ; ! "count"
sev:sev VTVINFL-CAUS_DIR-AOR_AR  ; ! "love"
seyret:seyre%{t%} VTVINFL-CAUS_DIR-AOR_AR  ; ! "watch"
sor:sor VTVINFL-CAUS_DIR-AOR_AR  ; ! "ask"
söyle:söyle VTVINFL-CAUS_T-AOR_IR  ; ! "tell"
var:var VIVINFL-CAUS_DIR-AOR_IR  ; ! "arrive"
ver:ver VTVINFL-CAUS_DIR-AOR_IR  ; ! "give up"
ye:ye VTVINFL-CAUS_DIR-AOR_IR  ; ! "ear"
al:al VTVINFL-CAUS_DIR-AOR_IR ; ! "take, buy"
al:al VTVINFL-CAUS_DIR-AOR_IR ; ! "take, buy"
ara:ar%{a%} VTVINFL-CAUS_T-AOR_IR ; ! "seek, search, look for"
bak:bak VTVINFL-CAUS_DIR-AOR_AR ; ! "look at"
bil:bil VTVINFL-CAUS_DIR-AOR_IR ; ! "know"
bit:bi%{t%} VIVINFL-CAUS_DIR-AOR_AR ; ! "finish"
bul:bul VTVINFL-CAUS_DIR-AOR_IR ; ! "find"
çalış:çalış VIVINFL-CAUS_DIR-AOR_IR ; ! "study, work"
de:de VIVINFL-CAUS_DIR-AOR_IR ; ! ""
de:d%{e%} VTVINFL-CAUS_T-AOR_IR ; ! "say"
dinle:dinl%{e%} VTVINFL-CAUS_T-AOR_IR ; ! "listen"
düşün:düşün VTVINFL-CAUS_DIR-AOR_IR ; ! "think"
duy:duy VTVINFL-CAUS_DIR-AOR_AR ; ! "hear"
duy:duy VIVINFL-CAUS_DIR-AOR_AR ; ! "hear"
gel:gel VIVINFL-CAUS_DIR-AOR_IR ; ! "come to a stop"
gezin:gezin VIVINFL-CAUS_DIR-AOR_IR ; ! "hang around"
git:gi%{t%} VIVINFL-CAUS_DIR-AOR_AR ; ! "go"
gör:gör VTVINFL-CAUS_DIR-AOR_IR ; ! "see"
görün:görün VIVINFL-CAUS_DIR-AOR_IR ; ! "appear"
gül:gül VIVINFL-CAUS_DIR-AOR_AR ; ! "laugh"
iç:iç VTVINFL-CAUS_DIR-AOR_AR ; ! "drink"
iste:iste VTVINFL-CAUS_T-AOR_IR ; ! "want"
bakın:bakın VTVINFL-CAUS_T-AOR_IR ; ! "??"

LEXICON Interjections

tabi% ki:tabi% ki INTERJ ; ! ""
tabii% ki:tabii% ki INTERJ ; ! ""
şöyle:şöyle INTERJ ; ! ""
peki:peki INTERJ ; ! ""
evet:evet INTERJ ; ! "YES!"
hayır:hayır INTERJ ; ! "NO!"

LEXICON Particles 

mi%<qst%>:mi # ; 
mi%<qst%>:mu # ; ! Dir/LR
mi%<qst%>:mü # ; ! Dir/LR 
mi%<qst%>:mı # ; ! Dir/LR 

LEXICON Punctuation

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ; 
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;

<(%0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9)+>  DIGITLEX ; ! Use/Circ

